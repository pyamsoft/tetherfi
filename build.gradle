/*
 * Copyright 2021 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext {
    kotlin_version = '1.7.10'
    desugar = "1.2.2"
    compose_version = "1.2.1"
    compose_compiler_version = "1.3.2"
  }

  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://jitpack.io"
      content {
        includeGroup("com.github.pyamsoft.pydroid")
        includeGroup("com.github.pyamsoft")
      }
    }

    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  ext {
    minSdk = 23
    targetSdk = 32
    compileSdk = 33

    // Us
    pydroid = "25.0.0"
    cachify = "0.1.0"
    highlander = "0.1.0"

    // Leak Canary
    leakCanary = "2.9.1"

    // Logging
    timber = "5.0.1"

    // Dagger
    dagger = "2.44"

    // Compose
    composeActivity = "1.6.0"
    accompanist = "0.25.1"
    coil = "2.2.2"

    // AndroidX
    core = "1.9.0"
    fragment = "1.5.3"
    appCompat = "1.5.1"

    // Material Design
    materialDesign = "1.6.1"

    ktor = "2.1.2"
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.3.0'

    // Gradle version checker
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.42.0'

    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    // Spotless
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.11.0"

    //        classpath 'com.google.gms:google-services:4.3.2'
  }
}

plugins {
  // Fix Android build cache
  // https://github.com/gradle/android-cache-fix-gradle-plugin
  id "org.gradle.android.cache-fix" version "2.5.7" apply false
}

subprojects {
  plugins.withType(com.android.build.gradle.api.AndroidBasePlugin) {
    project.apply plugin: "org.gradle.android.cache-fix"
  }
}

allprojects {
  apply plugin: "com.github.ben-manes.versions"
  apply plugin: "com.diffplug.spotless"

  spotless {
    java {
      target "src/**/*.java"

      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    kotlin {
      target "src/**/*.kt"
      ktfmt()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
    groovyGradle {
      target "*.gradle"
      greclipse()

      trimTrailingWhitespace()
      endWithNewline()
      indentWithSpaces(2)
    }
  }

  // Ignore non-release libraries
  def isNonStable = { version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
  }

  tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
      isNonStable(it.candidate.version)
    }
  }

  repositories {
    mavenLocal()
    google()
    mavenCentral()

    maven {
      url "https://jitpack.io"
      content {
        includeGroup("com.github.pyamsoft.pydroid")
        includeGroup("com.github.pyamsoft")
      }
    }

    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    options.deprecation = true
  }
}
